?start: gherkin_document

gherkin_document: feature?

//Feature! := FeatureHeader Background? ScenarioDefinition* Rule*
//FeatureHeader! := #Language? Tags? #FeatureLine DescriptionHelper

feature: _NL* feature_header? feature_line scenarios

scenarios: [_INDENT scenario* _DEDENT]

feature_header: tag_line*
feature_line: "Feature: " string _NL?

scenario: scenario_line [_INDENT steps _DEDENT]
scenario_line: "Scenario: " string _NL?

steps: givens? (whens | thens) *

givens: step{GIVEN}+ step{AND}*
whens: step{WHEN}+ step{AND}*
thens: step{THEN}+ step{AND}*

step{type}: step_line{type} (_INDENT step_arg _DEDENT)?
step_line{type}: type string _NL?

GIVEN: "Given"
WHEN: "When"
THEN: "Then"
AND: "And"


step_arg: [step_docstring _NL] [data_table]

data_table: data_table_row (_NL data_table_row)+ _NL
data_table_row: "|" (/.*?(?<!\\)(\\\\)*?\|/)+

tag: "@" CNAME
tag_line: tag (tag)* _NL

string: /[^\n]+/
// TODO: Probably the following does not need the negative lookbehind part
//step_docstring: /"""\n.*?(?<!\\)(\\\\)*?\n\s*"""/is
//              | /'''\n.*?(?<!\\)(\\\\)*?\n\s*'''/is

step_docstring: /"""\n.*?\n\s*"""/is

// %import common.ESCAPED_STRING -> string
%import common.WS_INLINE
%import common.LETTER
%import common.NUMBER
%import common.CNAME

%declare _INDENT _DEDENT
%ignore WS_INLINE

_NL: /(\r?\n[\t ]*)+/
