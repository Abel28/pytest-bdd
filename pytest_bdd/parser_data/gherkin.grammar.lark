?start: gherkin_document

?gherkin_document: _NL* (feature? | _INDENT feature? _DEDENT)

//Feature! := FeatureHeader Background? ScenarioDefinition* Rule*
//FeatureHeader! := #Language? Tags? #FeatureLine DescriptionHelper

feature: [tag_lines] feature_line [_INDENT [description] [background] [scenarios] _DEDENT]

description: (STRING _NL)+

background: background_line [_INDENT steps _DEDENT]
background_line: BACKGROUND [STRING] _NL?

scenarios: scenario*

feature_header: [tag_lines]
feature_line: FEATURE STRING _NL?

scenario: [tag_lines] scenario_line [_INDENT [steps] [examples] _DEDENT]
scenario_line: SCENARIO STRING _NL?

examples: example_line example_table
example_line: EXAMPLE [STRING] _NL?

example_table: example_table_row example_table_row+
example_table_row: /\|.*\|/ _NL


steps: givens? (whens | thens) *

givens: step{GIVEN}+ step{AND}*
whens: step{WHEN}+ step{AND}*
thens: step{THEN}+ step{AND}*

// step{type}: step_line{type} (_INDENT step_arg _DEDENT)?
step{type}: step_line{type}
step_line{type}: type STRING _NL?


step_arg: [step_docstring _NL] [data_table]

data_table: data_table_row (_NL data_table_row)+ _NL
data_table_row: "|" (/.*?(?<!\\)(\\\\)*?\|/)+

tag: /@[^\s]+/
tag_line: tag+ _NL
tag_lines: tag_line+

//string: /[^\n]+/
// anything but new lines and comments.
// Use low priority (0), so that we attempt to first match other terminals with priority 1.
// This is needed to avoid having "description" greedy matching other rules like "background" and "scenarios"
STRING.0: /[^\n#]+/

GIVEN.1: "Given"
WHEN.1: "When"
THEN.1: "Then"
AND.1: "And"

SCENARIO.1: "Scenario:"
BACKGROUND.1: "Background:"
FEATURE.1: "Feature:"
EXAMPLE.1: "Examples:"

// TODO: Probably the following does not need the negative lookbehind part
//step_docstring: /"""\n.*?(?<!\\)(\\\\)*?\n\s*"""/is
//              | /'''\n.*?(?<!\\)(\\\\)*?\n\s*'''/is

step_docstring: /"""\n.*?\n\s*"""/is

// %import common.ESCAPED_STRING -> string
%import common.WS_INLINE
%import common.LETTER
%import common.NUMBER
%import common.CNAME

%declare _INDENT _DEDENT
%ignore WS_INLINE

COMMENT: /#[^\n]*/
%ignore COMMENT

// _NL: /(\r?\n[\t ]*)+/

_NL: ( /\r?\n[\t ]*/ | COMMENT )+
